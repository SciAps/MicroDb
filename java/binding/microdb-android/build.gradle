
buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
  compileSdkVersion 19
  buildToolsVersion "20.0.0"

  defaultConfig {
    minSdkVersion 9
    targetSdkVersion 19
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      //minifyEnabled true
      //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  /*
  sourceSets {
    main {
      java {
        srcDir '../runtime/src/main/java'
      }
    }
  }
  */
}

dependencies {
  compile 'org.slf4j:slf4j-android:1.7.7'
  compile project(':microdb-runtime')
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts() {
    archives androidSourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'microdb-android'
            description 'MicroDB for Android'
            url 'http://github.com/dinocore1/MicroDb'

            scm {
                url 'scm:git@github.com:dinocore1/MicroDb.git'
                connection 'scm:git@github.com:dinocore1/MicroDb.git'
                developerConnection 'scm:git@github.com:dinocore1/MicroDb.git'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/dinocore1/MicroDb/blob/master/LICENSE.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'psoucy'
                    name 'Paul Soucy'
                }
            }
        }

    }
}